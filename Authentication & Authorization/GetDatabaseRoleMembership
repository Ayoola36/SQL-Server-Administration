/*
	Returns a list of database principals (users, et al) and the roles
	they are members of (if any) for all system and user databases.
*/

CREATE TABLE #UserRoleMembership (
	[Database Name] SYSNAME,
	[Principal Name] SYSNAME,
	[Principal Type] CHAR(1),
	[Role Name] SYSNAME
)

DECLARE @command VARCHAR(1000);

--Database users and roles (if any) they are members of.
SELECT @command = 'USE [?];

INSERT INTO #UserRoleMembership
SELECT DB_NAME() AS DatabaseName, 
	u.name, 
	u.type,
	COALESCE(r.name, ''<none>'') AS RoleName
FROM sys.database_principals u
LEFT JOIN sys.database_role_members rm
	ON rm.member_principal_id = u.principal_id
LEFT JOIN sys.database_principals r
	ON r.principal_id = rm.role_principal_id
--WHERE u.type != ''R'' ';

EXEC sp_MSforeachdb @command;

SELECT m.[Database Name], 
	m.[Principal Name],
	CASE m.[Principal Type]
		WHEN 'A' THEN 'Application role'
		WHEN 'C' THEN 'User mapped to a certificate'
		WHEN 'E' THEN 'External user from Azure Active Directory'
		WHEN 'G' THEN 'Windows group'
		WHEN 'K' THEN 'User mapped to an asymmetric key'
		WHEN 'R' THEN 'Database role'
		WHEN 'S' THEN 'User (SQL Auth)'
		WHEN 'U' THEN 'User (Windows Auth)'
		WHEN 'X' THEN 'External group from Azure Active Directory group or applications'
	END	AS [Principal Description],
	m.[Role Name]
FROM #UserRoleMembership m
ORDER BY m.[Database Name], m.[Principal Name], m.[Role Name]

DROP TABLE #UserRoleMembership;
GO
